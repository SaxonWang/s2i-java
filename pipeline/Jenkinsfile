#!groovy

node('maven') {
    // define commands
    echo "start pipeline for app: ${env.JOB_NAME}"
    
    configFileProvider(
            [configFile(fileId: 'maven-settings', variable: 'MAVEN_SETTINGS')]) {

        // initialization
        def mvnCmd = "mvn -s $MAVEN_SETTINGS"
        def tokens = "${env.JOB_NAME}".tokenize('/')
        def project = tokens[0]
        def app = tokens[1]
        def projectDev = "${project}-dev"
        def projectStage = "${project}-stage"
        echo "project: ${project}, app: ${app}"
        echo "mvn: ${mvnCmd}"

        stage ('Build') {
            pullFromGit("master", "gitlab-jenkins-ci", "http://gitlab.your.com")
            buildWithMaven(mvnCmd)
        }

        stage ('Run Unit Test and Analysis') {
            parallel (
                'Test': {
                    runUnitTest(mvnCmd)
                },
                'Static Analysis': {
                    analysis(mvnCmd, "http://sonarqube:9000")
                }
            )
        }

        stage ('Push to Nexus') {
            pushToNexus(mvnCmd)
        }
        
        def ver = version()
        stage ('Deploy to Dev') {
            deploy(project, app, ver, "dev", "Asia/Shanghai")
        }

        stage ('Deploy to Stage') {
            timeout(time:60, unit:'MINUTES') {
                input message: "Promote to STAGE?", ok: "Promote"
            }
            deploy(project, app, ver, "stage", "Asia/Shanghai")
        }
    }
}

def pullFromGit(branch, credentialsId, url) {
    git branch: "${branch}", credentialsId: "${credentialsId}", url: "${url}/${env.JOB_NAME}.git"
}

def buildWithMaven(mvnCmd) {
    sh "${mvnCmd} clean install -Dmaven.test.skip=true"
}

def pushToNexus(mvnCmd) {
    sh "${mvnCmd} deploy -Dmaven.test.skip=true"
}

def runUnitTest(mvnCmd) {
    sh "${mvnCmd} test"
}

def analysis(mvnCmd, url) {
    sh "${mvnCmd} jacoco:report sonar:sonar -Dsonar.host.url=${url} -Dmaven.test.skip=true"
}

def deploy(project, app, ver, domain, timezone) {
    def projectDomain = "${project}-${domain}"
    def imageTag = "latest"
    if ( "dev" == domain ) {
        sh "rm -rf oc-build && mkdir -p oc-build"
        sh "cp target/${app}-${ver}.jar oc-build/app.jar"
        sh "oc project ${projectDomain}"
        // create build. override the exit code since it complains about exising imagestream
        if ( !isExist("bc", app, projectDomain) ) {
            sh "oc new-build --name=${app} --image-stream=s2i-java --binary=true --labels=app=${app} -n ${projectDomain} || true"
        }
        // build image
        sh "oc start-build ${app} --from-dir=oc-build --wait=true -n ${projectDomain}"
    } else {
        // tag for stage or prod
        imageTag = "${ver}"
        sh "oc tag ${project}-dev/${app}:latest ${projectDomain}/${app}:${imageTag}"
        sh "oc project ${projectDomain}"
    }

    // deploy image
    if ( !isExist("dc", app, projectDomain) ) {
        sh "oc new-app ${app}:${imageTag} --env TZ=${timezone} -n ${projectDomain}"
    }
    if ( !isExist("route", app, projectDomain) ) {
        sh "oc expose svc/${app} --port=8080 -n ${projectDomain}"
    }
}

def version() {
    def matcher = readFile('pom.xml') =~ '<version>(.+)</version>'
    matcher ? matcher[0][1] : null
}

def execShell(script) {
    echo "script: ${script}"
    
    def result = sh(returnStdout: true, script: "${script}").trim()
    echo "result: ${result}"
    return result
}

def isExist(resource, app, prj) {
    def result = execShell("oc get ${resource} -n ${prj} | grep ${app} | wc -l")
    echo "result: ${result}"
    return result != "0" ? true : false
}
